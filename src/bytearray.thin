function ByteArray_set16(array: ByteArray, index: int, value: int): void {
  array.set(index, value as byte);
  array.set(index + 1, (value >> 8) as byte);
}

function ByteArray_set32(array: ByteArray, index: int, value: int): void {
  array.set(index, value as byte);
  array.set(index + 1, (value >> 8) as byte);
  array.set(index + 2, (value >> 16) as byte);
  array.set(index + 3, (value >> 24) as byte);
}

function ByteArray_append32(array: ByteArray, value: int): void {
  array.append(value as byte);
  array.append((value >> 8) as byte);
  array.append((value >> 16) as byte);
  array.append((value >> 24) as byte);
}

#if JS

  declare function Uint8Array_new(length: int): Uint8Array;

  declare class Uint8Array {
    length: int;
    set(source: Uint8Array): void;
    subarray(start: int, end: int): Uint8Array;
    operator [] (index: int): byte;
    operator []= (index: int, value: byte): void;
  }

  function ByteArray_setString(array: ByteArray, index: int, text: string): void {
    var length = text.length;
    assert(index >= 0 && index + length * 2 <= array.length());
    var data = array._data;
    var i = 0;
    while (i < length) {
      var c = text[i];
      data[index] = c as byte;
      data[index + 1] = (c >> 8) as byte;
      index = index + 2;
      i = i + 1;
    }
  }

  extern class ByteArray {
    _data: Uint8Array;
    _length: int;

    length(): int {
      return this._length;
    }

    clear(): void {
      this._length = 0;
    }

    get(index: int): byte {
      assert((index as uint) < (this._length as uint));
      return this._data[index];
    }

    set(index: int, value: byte): void {
      assert((index as uint) < (this._length as uint));
      this._data[index] = value;
    }

    append(value: byte): void {
      var index = this._length;
      this.resize(index + 1);
      this._data[index] = value;
    }

    resize(length: int): void {
      if (length > (this._data != null ? this._data.length : 0)) {
        var capacity = length * 2;
        var data = Uint8Array_new(capacity);
        if (this._data != null) data.set(this._data);
        this._data = data;
      }

      this._length = length;
    }
  }

#elif WASM || C

  function ByteArray_setString(array: ByteArray, index: int, text: string): void {
    var length = text.length;
    unsafe {
      assert(index >= 0 && index + length * 2 <= array.length());
      memcpy(array._data + index, text as *byte + 4, length as uint * 2);
    }
  }

  extern class ByteArray {
    _data: *byte;
    _length: uint;
    _capacity: uint;

    length(): int {
      return this._length as int;
    }

    unsafe bytes(): *byte {
      return this._data;
    }

    clear(): void {
      this._length = 0;
    }

    get(index: int): byte {
      assert((index as uint) < this._length);
      unsafe {
        return *(this._data + index);
      }
    }

    set(index: int, value: byte): void {
      assert((index as uint) < this._length);
      unsafe {
        *(this._data + index) = value;
      }
    }

    append(value: byte): void {
      var offset = this._length;
      unsafe {
        this.resize(offset as int + 1);
        *(this._data + offset) = value;
      }
    }

    resize(length: int): void {
      if (length as uint > this._capacity) {
        unsafe {
          var capacity = length as uint * 2;
          var data = malloc(capacity);
          memcpy(data, this._data, this._length);
          this._capacity = capacity;
          this._data = data;
        }
      }

      this._length = length as uint;
    }
  }

#else

  declare function ByteArray_setString(array: ByteArray, index: int, text: string): void;

  declare class ByteArray {
    length(): int;
    clear(): void;
    get(index: int): byte;
    set(index: int, value: byte): void;
    append(value: byte): void;
    resize(length: int): void;
  }

#endif
